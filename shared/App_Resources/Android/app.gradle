
def BUILD_TOOLS_PATH = "$rootDir/build-tools"

android {
  compileSdkVersion 34

  defaultConfig {
    minSdkVersion 17
    targetSdkVersion 34

    // Version Information
    versionCode 1
    versionName "1.0.0"
    
    generatedDensities = []
  }

  aaptOptions {
    additionalParameters "--no-version-vectors"
  }
  compileOptions {
      // Flag to enable support for the new language APIs
      coreLibraryDesugaringEnabled false
      sourceCompatibility JavaVersion.VERSION_17
      targetCompatibility JavaVersion.VERSION_17
  }
  lintOptions {
      checkReleaseBuilds false
      // Or, if you prefer, you can continue to check for errors in release builds,
      // but continue the build even when errors are found:
      abortOnError false
  }
  buildTypes {
    release {
      multiDexEnabled false
      // Enables code shrinking, obfuscation, and optimization for only
      // your project's release build type.
      minifyEnabled true

      // Enables resource shrinking, which is performed by the
      // Android Gradle plugin.
      shrinkResources true

      // resConfigs "en", "fr"

      // Includes the default ProGuard rules files that are packaged with
      // the Android Gradle plugin. To learn more, go to the section about
      // R8 configuration files.
      def absolutePathToAppResources = getAppResourcesPath()
      proguardFiles getDefaultProguardFile(
        'proguard-android-optimize.txt'),
        "$BUILD_TOOLS_PATH/proguard-rules.pro"

      debuggable false
      jniDebuggable false
      renderscriptDebuggable false
    }
    debug {
      multiDexEnabled true
    }
  }
}

task createProguard {
    def scriptPath = "$USER_PROJECT_ROOT/../shared/scripts/createProguard.js"
    if (!file(scriptPath).exists()) {
        scriptPath = "$USER_PROJECT_ROOT/../../shared/scripts/createProguard.js"
    }
    dependsOn 'buildMetadata'
    doLast {
        exec {
            workingDir "$USER_PROJECT_ROOT"
            commandLine 'node', scriptPath
        }
    }
}
project.tasks.configureEach {
    if (name =~ /buildMetadata/) {
        it.finalizedBy(createProguard)
    }
    if (name =~ /minify(Debug|Release)WithR8/) {
        it.dependsOn(createProguard)
    }
}